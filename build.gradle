println """
*******************************************
 You are building CommandBook! 
 
 If you encounter trouble:
 1) Read CONTRIBUTING.md if you haven't yet
 2) Try running 'build' in a separate Gradle run
 3) Use gradlew and not gradle
 4) If you still need help, ask on Discord! https://discord.gg/enginehub
 
  Output files will be in [subproject]/build/libs
*******************************************
"""
buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }

    configurations.all {
        resolutionStrategy {
            force 'com.google.guava:guava:21.0'
        }
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.3"
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.7.5'
        classpath 'org.ajoberstar:gradle-git:1.7.2'
    }
}

if (!project.hasProperty("artifactory_contextUrl")) ext.artifactory_contextUrl = "http://localhost"
if (!project.hasProperty("artifactory_user")) ext.artifactory_user = "guest"
if (!project.hasProperty("artifactory_password")) ext.artifactory_password = ""

if (!project.hasProperty("gitCommitHash")) {
    try {
        def repo = org.ajoberstar.grgit.Grgit.open(project.file('.'))
        ext.gitCommitHash = repo.head().abbreviatedId
    } catch (Exception e) {
        ext.gitCommitHash = "no_git_id"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.jfrog.artifactory'

group = pluginGroup
version = pluginVersion
ext.internalVersion = version + ";" + gitCommitHash

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = 'sk89q-repo'
        url = 'http://maven.sk89q.com/repo/'
    }
    maven {
        name = 'Destroystokyo Repo'
        url = 'https://repo.destroystokyo.com/repository/maven-public/'
    }
}

dependencies {
    compile 'com.destroystokyo.paper:paper-api:1.15.1-R0.1-SNAPSHOT'
    compile 'com.sk89q.worldedit:worldedit-core:7.1.0-SNAPSHOT'
    compile 'com.sk89q.worldedit:worldedit-bukkit:7.1.0-SNAPSHOT'
    compile 'org.enginehub.jinglenote:jinglenote-core:1.0.0-SNAPSHOT'
    compile('org.enginehub.jinglenote:jinglenote-bukkit:1.0.0-SNAPSHOT') {
        exclude group: 'org.spigotmc', module: 'spigot-api'
    }
    compile 'com.zachsthings.libcomponents:libcomponents-bukkit:1.3.0-SNAPSHOT'
    compile 'net.sf.opencsv:opencsv:2.0'

    compileOnly 'com.sk89q.worldedit.worldedit-libs:ap:7.1.0-SNAPSHOT'
    annotationProcessor 'com.sk89q.worldedit.worldedit-libs:ap:7.1.0-SNAPSHOT'
    annotationProcessor "com.google.guava:guava:21.0"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(JavaCompile).configureEach {
    it.options.compilerArgs.add("-Aarg.name.key.prefix=")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives shadowJar
    archives sourcesJar
    archives javadocJar
}

build.dependsOn(sourcesJar)
build.dependsOn(javadocJar)

jar {
    manifest {
        attributes("Class-Path": "CommandBook/WorldEdit.jar lib/WorldEdit.jar ../lib/WorldEdit.jar ../WorldEdit.jar",
                   "WorldEdit-Version": version)
    }
}

shadowJar {
    classifier 'dist'

    dependencies {
        include(dependency('net.sf.opencsv:opencsv'))
        include(dependency('com.zachsthings.libcomponents:libcomponents-bukkit'))
        relocate ("org.enginehub.jinglenote", "com.sk89q.commandbook.enginehub.jinglenote") {
            include(dependency("org.enginehub.jinglenote:jinglenote-core"))
            include(dependency("org.enginehub.jinglenote:jinglenote-bukkit"))
        }
    }

    exclude 'GradleStart**'
    exclude '.cache'
    exclude 'LICENSE*'
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = project.version.contains("SNAPSHOT") ? 'libs-snapshot-local' : 'libs-release-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
    }

    resolve {
        repository {
            repoKey = 'repo'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}

artifactoryPublish {
    publishConfigs("archives")
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        expand 'internalVersion': project.internalVersion
        include 'plugin.yml'
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}
